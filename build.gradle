buildscript {
    ext {
        springBootVersion = '2.1.0.BUILD-SNAPSHOT'
    }
    repositories {
        jcenter()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.postgresql:postgresql:42.2.4'
    }
}
plugins {
    //  id "net.ltgt.apt" version "0.17"
//    id "net.ltgt.apt-idea" version "0.17"
    id 'nu.studer.jooq' version '3.0.1'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

jooq {
    MyProject(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/forum'
            user = 'postgres'
            password = 'quang'
            schema = 'public'
            properties {
                property {
                    key = 'ssl'
                    value = 'false'
                }
            }
        }
        generator {

            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                // ...
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
            }
            generate {
                relations = true
                springAnnotations = true
                deprecated = false
                fluentSetters = true
                // ...
            }
            target {
                packageName = 'com.springforum'
            }
        }
    }
}
lombok {
    version = '1.18.2'
    sha256 = ""
}

tasks.generateMyProjectJooqSchemaSource.with {
    javaExecSpec = { JavaExecSpec s ->
        s.jvmArgs '--add-modules', 'java.xml.bind'
    }
}
configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

bootRun {
    jvmArgs = ["-Xms25m", "-Xmx100m", "-XX:+UseG1GC", "-XX:+UseStringDeduplication",'-Xquickstart', "-noverify"]
}
test{
    jvmArgs = ["-Xms25m", "-Xmx100m", "-XX:+UseG1GC", "-XX:+UseStringDeduplication","-noverify","-XX:TieredStopAtLevel=1"]
}
dependencies {
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-jooq'
    compile 'org.springframework.boot:spring-boot-starter-validation'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.hibernate:hibernate-core:5.3.2.Final'
    compile 'org.zalando:problem-spring-web:0.23.0'
    compile 'com.github.javafaker:javafaker:0.12'
    compile 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20180219.1'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    runtime 'org.postgresql:postgresql'
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    //annotationProcessor "org.projectlombok:lombok:1.18.2"
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'org.springframework.security:spring-security-test'
    testCompileOnly 'org.projectlombok:lombok:1.18.0'
    jooqRuntime 'org.jooq:jooq-meta-extensions:3.11.3'
    jooqRuntime("org.hibernate:hibernate-core:5.3.3.Final")
    jooqRuntime 'org.postgresql:postgresql'
}
